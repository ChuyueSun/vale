include{:verbatim} "vale.i.dfy"
include{:verbatim} "vale64.i.dfy"
include{:verbatim}{:from BASE} "lib/util/operations.i.dfy"
include "decls.vad"

#verbatim
module x64_decls64_i
{
import opened x64_def_s
import opened x64_vale_i
import opened x64_vale64_i
import opened x64_decls_i
import opened operations_i
#endverbatim

procedure{:operand} Mem_in(base:opr, inline offset:int) returns(o:mem)
    reads
        mem;
    extern;

procedure{:instruction Ins(Add64(dst, src))} Add64Wrap(inout dst:opr64, src:mem64)
    modifies
        efl;
    ensures
        dst == old(BitwiseAdd64(dst, src));
        Cf(efl) == old(dst + src >= 0x1_0000_0000_0000_0000);
{
    reveal evalCodeOpaque;
    reveal x86_ValidState;
    reveal BitwiseAdd64;

    this := MaybeUpdateOk(old(this), this);
    ghost if (!this.ok)
    {
        this := this.(regs := this.regs[@dst.r := 0]);
    }
}

procedure{:instruction Ins(Mov64(dst, MakeHeapOp(src, offset)))} Load64(
    out dst:opr64,
        src:opr64,
        inline offset:int)
    reads
        mem;
    requires
        @dst is OReg;
        @src is OReg;
    requires
        ValidSrcAddr(mem, src + offset, 64);
    ensures
        ValidSrcAddr(mem, old(src) + offset, 64);
        dst == mem[old(src) + offset].v64;
{
    reveal evalCodeOpaque;
    reveal x86_ValidState;

    this := MaybeUpdateOk(old(this), this);
    ghost if (!this.ok)
    {
        this := this.(regs := this.regs[@dst.r := 0]);
    }
}

procedure{:instruction Ins(Mov64(MakeHeapOp(dst, offset), src))} Store64(
    dst:opr64,
    src:opr64,
    inline offset:int)
    modifies
        mem;
    requires
        @dst is OReg;
        @src is OReg;
    requires/ensures
        ValidDstAddr(mem, dst + offset, 64);
    ensures
        mem == old(mem)[dst + offset := Mem64(src)];
{
    reveal evalCodeOpaque;
    reveal x86_ValidState;
    ghost var v := old(src);
    this := MaybeUpdateOk(old(this), this);
}

procedure{:instruction Ins(AddLea64(dst, src1, src2))} AddLea64(out dst:opr64, src1:opr64, src2:opr64)
    requires
        src1 + src2 < 0x1_0000_0000_0000_0000;
    ensures
        dst == old(src1) + old(src2);
{
    reveal evalCodeOpaque;
    reveal x86_ValidState;
    this := MaybeUpdateOk(old(this), this);
}

procedure{:instruction Ins(Sub64(dst, src))} Sub64(inout dst:opr64, src:opr64)
    requires
        0 <= dst - src;
    modifies 
        efl;
    ensures
        dst == old(dst) - old(src);
{
    reveal evalCodeOpaque;
    reveal x86_ValidState;
    this := MaybeUpdateOk(old(this), this);

}

procedure{:instruction Ins(Sub64(dst, src))} Sub64Wrap(inout dst:opr64, src:opr64)
    modifies
        efl;
    ensures
        dst == old(BitwiseSub64(dst, src));
{
    reveal evalCodeOpaque;
    reveal x86_ValidState;
    reveal BitwiseSub64;
    this := MaybeUpdateOk(old(this), this);

}

procedure{:instruction Ins(AddCarry64(dst, src))} Adc64Wrap(inout dst:opr64, src:mem64)
    modifies
        efl;
    ensures
        dst == old(BitwiseAdd64(BitwiseAdd64(dst, src), if Cf(efl) then 1 else 0));
        Cf(efl) == old(dst + src + (if Cf(efl) then 1 else 0) >= 0x1_0000_0000_0000_0000);
{
    reveal evalCodeOpaque;
    reveal x86_ValidState;
    reveal BitwiseAdd64;
    this := MaybeUpdateOk(old(this), this);

    lemma_BitwiseAdd64EquivalentToAddMod2To64(old(dst), old(src));
}

procedure{:instruction Ins(IMul64(dst, src))} IMul64Wrap(inout dst:opr64, src:opr64)
    modifies
        efl;
    ensures
        dst == old(BitwiseMul64(dst, src));
{
    reveal evalCodeOpaque;
    reveal x86_ValidState;
    reveal BitwiseMul64;
    reveal BitwiseMul64hi;
    this := MaybeUpdateOk(old(this), this);

}

procedure{:instruction Ins(Mul64(src))} Mul64Wrap(src:opr64)
    modifies
        efl;
        rax;
        rdx;
    ensures
        rax == old(BitwiseMul64(rax, src));
        rdx == old(BitwiseMul64hi(rax, src));
{
    reveal evalCodeOpaque;
    reveal x86_ValidState;
    reveal BitwiseMul64;
    reveal BitwiseMul64hi;
    this := MaybeUpdateOk(old(this), this);

}

procedure{:instruction Ins(And64(dst, src))} And64(inout dst:opr64, src:opr64)
    modifies
        efl;
    ensures
        dst == old(BitwiseAnd64(dst, src));
{
    reveal evalCodeOpaque;
    reveal x86_ValidState;
    this := MaybeUpdateOk(old(this), this);

}

procedure{:instruction Ins(Shl64(dst, src))} Shl64(inout dst:opr64, src:opr64)
    modifies
        efl;
    requires
        @src is OConst || @src == OReg(X86Ecx);
    requires
        0 <= src < 64;
    ensures
        dst == old(BitwiseShl64(dst, src));
{
    reveal evalCodeOpaque;
    reveal x86_ValidState;
    this := MaybeUpdateOk(old(this), this);

}

procedure{:instruction Ins(Shr64(dst, src))} Shr64(inout dst:opr64, src:opr64)
    modifies
        efl;
    requires
        @src is OConst || @src == OReg(X86Ecx);
    requires
        0 <= src < 64;
    ensures
        dst == old(BitwiseShr64(dst, src));
{
    reveal evalCodeOpaque;
    reveal x86_ValidState;
    this := MaybeUpdateOk(old(this), this);

}

#verbatim
} // end module
#endverbatim
